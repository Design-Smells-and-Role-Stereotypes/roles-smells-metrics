apply plugin: 'com.android.application'

android {
    compileSdkVersion = 28
    buildToolsVersion = '29.0.2'

    defaultConfig {
        applicationId = "com.android.keepass"
        minSdkVersion 14
        targetSdkVersion 28

        versionCode = 192
        versionName = "2.5.9"

        testApplicationId = "com.keepassdroid.tests"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }


    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "store"

    productFlavors {
        general {
            dimension = "store"
        }

        play {
            dimension = "store"
        }
    }


}

dependencies {

    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation ('com.madgag.spongycastle:prov:1.58.0.0') {
        exclude group: 'junit'
    }
    implementation 'joda-time:joda-time:2.9.4'
    implementation 'androidx.core:core:1.2.0'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation "androidx.biometric:biometric:1.0.1"
    implementation "androidx.preference:preference:1.1.0"
    implementation "com.google.android.material:material:1.1.0"

    androidTestImplementation'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.14"
    }
}

apply plugin: "net.ltgt.errorprone"

tasks.withType(JavaCompile) {
    options.compilerArgs += [
        '-Xep:ClassCanBeStatic:OFF',
        '-Xep:DefaultCharset:OFF',
        '-Xep:EqualsHashCode:OFF',
        '-Xep:FragmentInjection:OFF',
        '-Xep:HidingField:OFF',
        '-Xep:JdkObsolete:OFF',
        '-Xep:MissingOverride:OFF',
        '-Xep:NonOverridingEquals:OFF',
        '-Xep:ShortCircuitBoolean:OFF',
    ]
}

configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.3.1'
}
